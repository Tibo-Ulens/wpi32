(* Lexical grammar *)

newline = ?r"\n"?;
whitespace = ?r"\s"?;

comment = ";", { ?r"[^\n]*"? };

string = '"', { ?r'[^"]'? | '\"' }, '"';
char = "'", ( ?r"[^']"? | "\'" ), "'";
number = decimal_number
	| hex_number
	| octal_number
	| binary_number;

decimal_number = ?r"[0-9]+"?;
hex_number = "0x", ?r"[a-fA-F0-9]+"?;
octal_number = "0o", ?r"[0-7]+"?;
binary_number = "0b", ?r"[0-1]+"?;

identifier_initial = ?r"[a-zA-Z!$&?^_~@?]"?;
identifier_subsequent = identifier_initial | ?r"0-9"?;

label = identifier_initial, { identifier_subsequent };
label_define = label, ":";
local_label = ".", label;
local_label_define = local_label, ":";

(* Syntactical grammar *)

root = { { whitespace }, statement, [ comment ], newline };
statement = label_define
	| local_label_define
	| directive
	| instruction;

directive = byte_directive
	| half_directive
	| word_directive
	| repeat_directive
	| equ_directive;

literal = string | char | immediate;

byte_directive = ".byte", literal, { ",", literal };
half_directive = ".half", literal, { ",", literal };
word_directive = ".word", literal, { ",", literal };

repeat_directive = ".repeat", immediate, directive;

equ_directive = ".equ", ( label | local_label ), literal;

immediate = xor_imm, { "|", xor_imm };
xor_imm = and_imm, { "^", and_imm};
and_imm = eq_imm, { "&", eq_imm};
eq_imm = ord_imm, { ( "==", ord_imm ) | ( "!=", ord_imm ) };
ord_imm = ord_imm, {
	("<", shift_imm )
	| ( "<=", shift_imm)
	| ( ">", shift_imm )
	| ( ">=", shift_imm )
};
shift_imm = addsub_imm, {
	( "<<", addsub_imm )
	| ( ">>", addsub_imm )
	| ( ">>>", addsub_imm )
};
addsub_imm = muldivrem_imm, {
	( "+", muldivrem_imm )
	| ( "-", muldivrem_imm )
};
muldivrem_imm = operand, {
	( "*", operand )
	| ( "/", operand )
	| ( "%", operand )
};
operand = label | local_label | number | ( "(", immediate, ")" );

address_calculation = "[", register, "+" | "-", immediate, "]";

instruction = integer_register_immediate_instruction
	| integer_register_register_instruction
	| upper_immediate_instruction
	| jump_instruction
	| jump_register_instruction
	| branch_instruction
	| load_instruction
	| store_instruction
	| memory_ordering_instruction
	| environment_instruction
	| fence_instruction
	| csr_register_instruction
	| csr_immediate_instruction
	| multiply_instruction
	| divide_instruction
	| remainder_instruction;

register = "r0" | "r1" | "r2" | "r3" | "r4" | "r5" | "r6" | "r7" | "r8" | "r9"
	| "r10" | "r11" | "r12" | "r13" | "r14" | "r15" | "r16" | "r17" | "r18"
	| "r19" | "r20" | "r21" | "r22" | "r23" | "r24" | "r25" | "r26" | "r27"
	| "r28" | "r29" | "r30" | "r31"
	| "zero" | "ra" | "sp" | "gp" | "tp" | "fp"
	| "t0" | "t1" | "t2" | "t3" | "t4" | "t5" | "t6"
	| "s0" | "s1" | "s2" | "s3" | "s4" | "s5" | "s6" | "s7" | "s8" | "s9"
	| "s10" | "s11"
	| "a0" | "a1" | "a2" | "a3" | "a4" | "a5" | "a6" | "a7";

integer_register_immediate_instruction =
	( "addi" | "slti" | "sltiu" | "andi" | "ori" | "xori" | "lsli" | "lsri" | "asri" ),
	register,
	register,
	immediate
;

integer_register_register_instruction =
	( "add" | "slt" | "sltu" | "and" | "or" | "xor" | "lsl" | "lsr" | "asr" | "sub" ),
	register,
	register,
	register
;

upper_immediate_instruction = ( "lui" | "auipc" ), register, immediate;

jump_instruction = "jal", register, immediate;
jump_register_instruction = "jalr", register, register, immediate;

branch_instruction =
	( "beq" | "bne" | "blt" | "bltu" | "bge" | "bgeu" ),
	register,
	register,
	immediate
;

load_instruction =
	( "lb" | "lbu" | "lh" | "lhu" | "lw" ),
	register,
	address_calculation
;
store_instruction =
	( "sb", "sh", "sw" ),
	register,
	address_calculation
;

memory_ordering_instruction =
	( "fence", ordering_operation, ordering_operation )
	| total_store_ordering_instruction;
ordering_operation = "i" | "o" | "r" | "w";
total_store_ordering_instruction = "fence.tso";

environment_instruction =
	( "ecall" | "ebreak" ),
	register,
	register
;

fence_instruction = "fence.i";

csr_register_instruction =
	( "csrrw"  | "csrrs" | "csrrc" ),
	register,
	register,
	register
;
csr_immediate_instruction =
	( "csrrwi"  | "csrrsi" | "csrrci" ),
	register,
	immediate,
	register
;

multiply_instruction =
	( "mul" | "mulh" | "mulhu" | "mulhsu" ),
	register,
	register,
	register
;
divide_instruction =
	( "div" | "divu" ),
	register,
	register,
	register
;
remainder_instruction =
	( "rem" | "remu" ),
	register,
	register,
	register
;
